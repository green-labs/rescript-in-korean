---
title: 'Interop Cheatsheet'
metaTitle: 'Interop Cheatsheet'
metaDescription: 'Cheatsheet for various interop scenarios in ReScript'
sourceUrl: 'https://rescript-lang.org/docs/manual/latest/interop-cheatsheet'
canonical: 'https://rescript-lang.org/docs/manual/latest/interop-cheatsheet'
---

<!-- TODO: this should be a glossary instead -->

## Raw JS

```reason
let add = %raw("(a, b) => a + b")
%%raw("const a = 1")
```

```js
var add = (a, b) => a + b;
const a = 1;
```

## Global Value

```reason
@bs.val external setTimeout: (unit => unit, int) => float = "setTimeout"
```

```js
// Empty output
```

## Global Module's Value

```reason
@bs.val @bs.scope("Math")
external random: unit => float = "random"

let someNumber = random()

@bs.val @bs.scope(("window", "location", "ancestorOrigins"))
external length: int = "length"
```

```js
var someNumber = Math.random();
```

## Nullable

```reason
let a = Some(5) // compiles to 5
let b = None // compiles to undefined
```

```js
var a = 5;
var b;
```

Handling a value that can be `undefined` and `null`, by ditching the `option` type and using `Js.Nullable.t`:

```reason
let jsNull = Js.Nullable.null
let jsUndefined = Js.Nullable.undefined
let result1: Js.Nullable.t<string> = Js.Nullable.return("hello")
let result2: Js.Nullable.t<int> = Js.Nullable.fromOption(Some(10))
let result3: option<int> = Js.Nullable.toOption(Js.Nullable.return(10))
```

```js
var Caml_option = require('./stdlib/caml_option.js');
var Js_null_undefined = require('./stdlib/js_null_undefined.js');

var jsNull = null;
var jsUndefined;
var result1 = 'hello';
var result2 = Js_null_undefined.fromOption(10);
var result3 = Caml_option.nullable_to_opt(10);
```

## JS Object

- [Bind to a JS object as a ReScript record](bind-to-js-object#bind-to-record-like-js-objects).
- [Bind to a JS object that acts like a hash map](bind-to-js-object#bind-to-hash-map-like-js-object).
- [Bind to a JS object that's a class](bind-to-js-object#bind-to-a-js-object-thats-a-class).

## Function

### Object Method & Chaining

```reason
@bs.send external map: (array<'a>, 'a => 'b) => array<'b> = "map"
@bs.send external filter: (array<'a>, 'a => 'b) => array<'b> = "filter"
[1, 2, 3]
  ->map(a => a + 1)
  ->filter(a => mod(a, 2) == 0)
  ->Js.log
```

```js
console.log(
  [1, 2, 3]
    .map(function(a) {
      return (a + 1) | 0;
    })
    .filter(function(a) {
      return a % 2 === 0;
    })
);
```

### Variadic Arguments

```reason
@bs.module("path") @bs.variadic
external join: array<string> => string = "join"
```

```js
// Empty output
```

### Polymorphic Function

```reason
@bs.module("Drawing") external drawCat: unit => unit = "draw"
@bs.module("Drawing") external drawDog: (~giveName: string) => unit = "draw"
```

```js
// Empty output
```

```reason
@bs.val
external padLeft: (
  string,
  @bs.unwrap [
    | #Str(string)
    | #Int(int)
  ])
  => string = "padLeft"

padLeft("Hello World", #Int(4))
padLeft("Hello World", #Str("Message from ReScript: "))
```

```js
padLeft('Hello World', 4);
padLeft('Hello World', 'Message from ReScript: ');
```

## JS Module Interop

[See here](import-from-export-to-js.md)

## Dangerous Type Cast

Final escape hatch converter. Do not abuse.

```reason
external convertToFloat: int => float = "%identity"
let age = 10
let gpa = 2.1 +. convertToFloat(age)
```

```js
var age = 10;
var gpa = 2.1 + 10;
```

## List of @bs Decorators

- `@bs.as`
- `@bs.get`
- `@bs.get_index`
- `@bs.inline`
- `@bs.int`
- `@bs.meth`
- `@bs.module`
- `@bs.new`
- `@bs.return`
- `@bs.scope`
- `@bs.send`
- `@bs.set`
- `@bs.set_index`
- `@bs.string`
- `@bs.this`
- `@bs.uncurry`
- `@bs.unwrap`
- `@bs.val`
- `@bs.variadic`
