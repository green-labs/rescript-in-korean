---
title: 'Interop Cheatsheet'
metaTitle: 'Interop Cheatsheet'
metaDescription: 'Cheatsheet for various interop scenarios in ReScript'
sourceUrl: 'https://rescript-lang.org/docs/manual/latest/interop-cheatsheet'
canonical: 'https://rescript-lang.org/docs/manual/latest/interop-cheatsheet'
---

<!-- TODO: this should be a glossary instead -->

## Raw JS

```reason
let add = %raw("(a, b) => a + b")
%%raw("const a = 1")
```

```js
var add = (a, b) => a + b;
const a = 1;
```

## Global Value

```reason
@bs.val external setTimeout: (unit => unit, int) => float = "setTimeout"
```

```js
// Empty output
```

## Global Module's Value

```reason
@bs.val @bs.scope("Math")
external random: unit => float = "random"

let someNumber = random()

@bs.val @bs.scope(("window", "location", "ancestorOrigins"))
external length: int = "length"
```

```js
var someNumber = Math.random();
```

## Nullable

```reason
let a = Some(5) // compiles to 5
let b = None // compiles to undefined
```

```js
var a = 5;
var b;
```

`option` 유형이 아닌 `Js.Nullable.t`를 사용하므로써, `undefined`과 `null`이 될수 있는 값들을 처리합니다

```reason
let jsNull = Js.Nullable.null
let jsUndefined = Js.Nullable.undefined
let result1: Js.Nullable.t<string> = Js.Nullable.return("hello")
let result2: Js.Nullable.t<int> = Js.Nullable.fromOption(Some(10))
let result3: option<int> = Js.Nullable.toOption(Js.Nullable.return(10))
```

```js
var Caml_option = require('./stdlib/caml_option.js');
var Js_null_undefined = require('./stdlib/js_null_undefined.js');

var jsNull = null;
var jsUndefined;
var result1 = 'hello';
var result2 = Js_null_undefined.fromOption(10);
var result3 = Caml_option.nullable_to_opt(10);
```

## 자바스크립트 객체

- [리스크립트 레코드를 사용해 바인딩하기](https://green-labs.github.io/rescript-in-korean/JavaScript-Interop/04-Bind-to-JS-Object#%EB%A0%88%EC%BD%94%EB%93%9C%EC%B2%98%EB%9F%BC%EC%82%AC%EC%9A%A9%ED%95%98%EB%8A%94%EC%9E%90%EB%B0%94%EC%8A%A4%ED%81%AC%EB%A6%BD%ED%8A%B8%EA%B0%9D%EC%B2%B4%EB%A5%BC%EB%B0%94%EC%9D%B8%EB%94%A9%ED%95%98%EA%B8%B0)
- [해시 맵처럼 사용하는 자바스크립트 객체를 바인딩하기](https://green-labs.github.io/rescript-in-korean/JavaScript-Interop/04-Bind-to-JS-Object#%ED%95%B4%EC%8B%9C%EB%A7%B5%EC%B2%98%EB%9F%BC%EC%82%AC%EC%9A%A9%ED%95%98%EB%8A%94%EC%9E%90%EB%B0%94%EC%8A%A4%ED%81%AC%EB%A6%BD%ED%8A%B8%EA%B0%9D%EC%B2%B4%EB%A5%BC%EB%B0%94%EC%9D%B8%EB%94%A9%ED%95%98%EA%B8%B0)
- [클래스인 자바스크립트 객체를 바인딩하기](https://green-labs.github.io/rescript-in-korean/JavaScript-Interop/04-Bind-to-JS-Object#%ED%81%B4%EB%9E%98%EC%8A%A4%EC%9D%B8%EC%9E%90%EB%B0%94%EC%8A%A4%ED%81%AC%EB%A6%BD%ED%8A%B8%EA%B0%9D%EC%B2%B4%EB%A5%BC%EB%B0%94%EC%9D%B8%EB%94%A9%ED%95%98%EA%B8%B0)

## 함수

### Object Method & Chaining

```reason
@bs.send external map: (array<'a>, 'a => 'b) => array<'b> = "map"
@bs.send external filter: (array<'a>, 'a => 'b) => array<'b> = "filter"
[1, 2, 3]
  ->map(a => a + 1)
  ->filter(a => mod(a, 2) == 0)
  ->Js.log
```

```js
console.log(
  [1, 2, 3]
    .map(function(a) {
      return (a + 1) | 0;
    })
    .filter(function(a) {
      return a % 2 === 0;
    })
);
```

### 가변 인수

```reason
@bs.module("path") @bs.variadic
external join: array<string> => string = "join"
```

```js
// Empty output
```

### 다형성 함수

```reason
@bs.module("Drawing") external drawCat: unit => unit = "draw"
@bs.module("Drawing") external drawDog: (~giveName: string) => unit = "draw"
```

```js
// Empty output
```

```reason
@bs.val
external padLeft: (
  string,
  @bs.unwrap [
    | #Str(string)
    | #Int(int)
  ])
  => string = "padLeft"

padLeft("Hello World", #Int(4))
padLeft("Hello World", #Str("Message from ReScript: "))
```

```js
padLeft('Hello World', 4);
padLeft('Hello World', 'Message from ReScript: ');
```

## 자바스크립트 모듈의 상호 운용성

[해당 링크에서 확인하세요](https://green-labs.github.io/rescript-in-korean/JavaScript-Interop/06-Import-from-Export-to-JS)

## Dangerous Type Cast

최후의 변환 방식입니다. 남용하지 마세요!

```reason
external convertToFloat: int => float = "%identity"
let age = 10
let gpa = 2.1 +. convertToFloat(age)
```

```js
var age = 10;
var gpa = 2.1 + 10;
```

## List of @bs Decorators

- `@bs.as`
- `@bs.get`
- `@bs.get_index`
- `@bs.inline`
- `@bs.int`
- `@bs.meth`
- `@bs.module`
- `@bs.new`
- `@bs.return`
- `@bs.scope`
- `@bs.send`
- `@bs.set`
- `@bs.set_index`
- `@bs.string`
- `@bs.this`
- `@bs.uncurry`
- `@bs.unwrap`
- `@bs.val`
- `@bs.variadic`
